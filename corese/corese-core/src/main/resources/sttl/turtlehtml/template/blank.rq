#
# Turtle Pretty Printer
# Olivier Corby, Wimmics Inria I3S, 2013
#
template {
  if (?refer, concat(st:turtle(?in), " "), "[") 
  
  ibox {
    if (us:myerror(?in), 
        st:call-template(st:pperror, st:call-template(st:value, ?in)),
        st:call-template(st:value, ?in)
    )
  }
  
  if (?refer, " .", 
  if (?nested, "]", "] ."))
  
  # pretty printer of errors
  if (us:myerror(?in),
	st:call-template(st:pperrorsp, ?in),
  "")
}
where {
  ?in ?p ?y
  filter isBlank(?in)
  filter not exists { ?in rdf:first ?e }
  
  bind (exists { ?a ?q ?in } as ?nested)
  
  bind (
    ?nested 
    && ((st:isStart() && exists { ?a ?q ?in . ?b ?r ?in filter(?a != ?b) })
	|| exists { ?in ?q ?in }
       )
  as ?refer)
}
limit 1
