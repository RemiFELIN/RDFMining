#
# uri =  st:dyyyy-mm-dd
# day = dd ; month = mm
# query dbpedia for events occurring this day of month
#
prefix o:   <http://dbpedia.org/ontology/>
prefix cal: <http://ns.inria.fr/sparql-extension/calendar/>
@bind kg:values
construct { 
  ?x o:birthDate ?date ; rdfs:label ?l
} 
where {

  bind (substr(st:get(st:uri), strlen(st:) + 2) as ?str)
  bind (coalesce(xsd:date(?str), "2000-02-29"^^xsd:date) as ?param)
  bind (month(?param) as ?month)
  bind (day(?param)   as ?day)

  service <http://fr.dbpedia.org/sparql/> { 
    select ?x  ?d ?month ?day ?l where {  
          values (?month ?day) { (UNDEF UNDEF) }
          ?x o:birthDate ?d  
          filter (datatype(?d) = xsd:date)         
          filter (month(?d) = ?month && day(?d) = ?day) 
          ?x rdfs:label ?l
          filter langMatches(lang(?l), "fr")
    } 
  }
  
  bind (cal:date(year(?d), month(?d), day(?d)) as ?date)
}
