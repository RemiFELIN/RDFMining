#
# Factorize all occurrences of same Union/Intersection
# into one occurrence everywhere
# [] owl:unionOf (?c1 ?c2 ?c3 ?c4) 
#
# Olivier Corby - Wimmics INRIA I3S - 2014 
#
delete {
  ?a ?r ?b  
  ?b ?ui ?b1
  ?b1 rdf:first ?c1 ; rdf:rest ?b2
  ?b2 rdf:first ?c2 ; rdf:rest ?b3
  ?b3 rdf:first ?c3 ; rdf:rest ?b4
  ?b4 rdf:first ?c4 ; rdf:rest rdf:nil
}
insert {
  ?a ?r ?y
}
where {
  # ?y is the representant of the set of similar U/I
  {select (sample(?x) as ?y) ?ui ?c1 ?c2 ?c3 ?c4
  where {    
    values ?ui { owl:unionOf owl:intersectioOf }    
    ?x ?ui (?c1 ?c2 ?c3 ?c4)
    }
    group by ?ui ?c1 ?c2 ?c3 ?c4
  }
  
  ?b1 rdf:first ?c1 ; rdf:rest ?b2
  ?b2 rdf:first ?c2 ; rdf:rest ?b3
  ?b3 rdf:first ?c3 ; rdf:rest ?b4
  ?b4 rdf:first ?c4 ; rdf:rest rdf:nil
  ?b ?ui ?b1
  filter(?b != ?y)
  
  ?a ?r ?b
}
