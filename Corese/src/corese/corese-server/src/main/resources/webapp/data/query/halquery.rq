#
# 
#
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dc:   <http://purl.org/dc/terms/>
@bind kg:filter
construct {
    ?uri ?p ?y . ?x ?q ?uri .
    ?y ?pname ?label . ?x ?pname ?label .
    ?y ?q ?z 
}
where {
    bind (st:get(st:uri) as ?uri)
    
    service <https://data.archives-ouvertes.fr/sparql> {
        select * where {
            { ?uri ?p ?y  
                optional { ?y foaf:name|skos:prefLabel|skos:altLabel|dc:title ?label ; ?pname ?label }
                optional { filter isBlank(?y) ?y ?q ?z  filter (! isBlank(?z)) }
            } 
            union 
            { ?x ?q ?uri  filter isURI(?x)
                optional { ?x foaf:name|skos:prefLabel|skos:altLabel ?label ; ?pname ?label } 
            }
        } 
        limit 10000
    }
    
    bind (us:link(?y) as ?a)
    bind (us:link(?x) as ?b)
}

function us:link(?s) {
    if (isURI(?s)) {
        st:setlink(?s, st:hlink(?s))
    }
}


