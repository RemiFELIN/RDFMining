prefix dqv:     <http://www.w3.org/ns/dqv#>
prefix kgi:     <http://ns.inria.fr/kg/index#>
prefix void:    <http://rdfs.org/ns/void#>
prefix dcterms: <http://purl.org/dc/terms/>

@timeout 10000
@report @skip
select distinct  ?serv ?cc (xt:get(js:report(1), "server") as ?name)
where {

service <http://prod-dekalog.inria.fr/sparql> {
    select distinct ?serv  where {
        graph ?g {
            ?metadata dcterms:modified ?modif .
            ?metadata kgi:curated ?s .
            ?s void:sparqlEndpoint  ?serv
        }
    }
    order by ?serv
}

{select ?serv (sum(?c) as ?cc)
where {
service ?serv {
    select ?p (count(*) as ?c) 
    where {?s ?p ?o} 
    group by ?p
}
} group by ?serv
}

}
order by  if (bound(?name), 0, 1) ?name desc(?cc) ?serv 

#  results
